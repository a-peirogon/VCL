[script]
n0=on 1:start:{
n1=  unset %BNCserver
n2=  set %servermsg 0
n3=  write -c servermsg.ini
n4=  .remini psyBNC.ini user
n5=  if ($readini(psyBNC.ini,psyBNC,enabled) == on) socklisten bouncer $readini(psyBNC.ini,psyBNC,port)
n6=}
n7=on 1:socklisten:bouncer:if (!$sock(bnc)) sockaccept bnc
n8=on 1:sockread:bnc:{
n9=  sockread %read
n10=  tokenize 32 %read
n11=  if (%BNCserver == ON) {
n12=    set %PONG $ctime
n13=    .timerpingpong 1 90 pingpong
n14=  }
n15=  if ($1 == NICK) {
n16=    if (%BNCserver != ON) set %bncnick $2
n17=    else if ($2 != $me) {
n18=      set %chnick $iif($chr(58) isin $2,$right($2,-1),$2)
n19=      nick $iif($chr(58) isin $2,$right($2,-1),$2)
n20=      set %chnick ON
n21=    }
n22=  }
n23=  else if ($1 == USER) {
n24=    if ($2 == $readini(psyBNC.ini,psyBNC,user)) {
n25=      if (%BNCserver != ON) {
n26=        sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Bine ati venit la erty psyBNC
n27=        sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Pentru a va conecta tastati /PASS parola
n28=      }
n29=      writeini psyBNC.ini User ident $2
n30=      set %bncident ~ $+ $2
n31=      writeini psyBNC.ini User mail $right($left($3,-1),-1)
n32=      set %bncmail $right($left($3,-1),-1)
n33=      writeini psyBNC.ini User IP $right($left($4,-1),-1)
n34=      set %bncIP $right($left($4,-1),-1)
n35=      writeini psyBNC.ini User name $right($5,-1)
n36=      set %bncname $right($5,-1)
n37=      set %fulladdress %bncnick $+ ! $+ $2 $+ @ $+ $right($left($4,-1),-1)
n38=    }
n39=  }
n40=  else if ($1 == PASS) {
n41=    if ($keydecrypt($2,$readini(psyBNC.ini,psyBNC,pass)) === TheBestBotIEverSeenAndTheBestBotIEverMade) {
n42=      set %fulladdress %bncnick $+ ! $+ %bncident $+ @ $+ %bncIP
n43=      sockwrite -n bnc : $+ %fulladdress NICK : $+ $me
n44=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Looking up your hostname... (bla bla bla, oare chiar foloseste la ceva asta???)
n45=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Hostname not found (using ip address) (dar asta???)
n46=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Acesta este ceea ce ar trebui sa se numeasca un psyBNC (nu?)
n47=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Daca a-ti mai folosit deja asa ceva, atunci e OK pentru ca este aproximativ la fel
n48=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Avand in vedere ca acest program imita un psyBNC real, veti avea la dispozitie aproape toate comenzile unui psyBNC real
n49=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Pentru o lista de comenzi tastati /BHELP
n50=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Setarile botului pot fi modificare si de aici. Pentru mai multe informatii tastati /BOTHELP
n51=      var %i 1
n52=      while (%i <= $lines(servermsg.ini)) {
n53=        tokenize 32 $read(servermsg.ini,%i)
n54=        if ($1 != 433) sockwrite -n bnc :psyBNC $1 %bncnick : $+ $replace($2-,C124,$chr(124))
n55=        inc %i
n56=      }
n57=      sockwrite -n bnc : $+ %fulladdress NICK : $+ $me
n58=      .timerchecknick 1 5 checkbncnick $me
n59=      if ($server) away
n60=      set %BNCserver ON
n61=      if ($server != $null) .timer 1 5 rj
n62=      if ($findfile(logs\,*.*,0) != 0) sockwrite -n bnc :psyBNC NOTICE %bncnick :Ai $findfile(logs\,*.*,0) mesaje noi!
n63=      else sockwrite -n bnc :psyBNC NOTICE %bncnick :Nu ai nici un mesaj nou!
n64=      .timercnick 1 5 nick %bncnick
n65=    }
n66=    else {
n67=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** Parola gresita
n68=      sockclose bnc
n69=    }
n70=  }
n71=  else if (%BNCserver) {
n72=    if ($1 == USERHOST) {
n73=      sockwrite -n bnc :psyBNC 302 %bncnick : $+ %bncnick $+ = $+ + $+ %bncident $+ @ $+ %bncip
n74=    }
n75=    else if ($1 == BCONNECT) {
n76=      server $2-
n77=    }
n78=    else if ($1 == NEWPASS) {
n79=      if ($2 == $null) {
n80=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/NEWPASS <parola> pentru a schimba parola
n81=        halt
n82=      }
n83=      writeini psyBNC.ini psyBNC pass $keycrypt($2,TheBestBotIEverSeenAndTheBestBotIEverMade)
n84=      sockwrite -n bnc :psyBNC NOTICE %bncnick :Parola a fost schimbata cu success!
n85=    }
n86=    else if ($1 == BOTHELP) {
n87=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** psyBNC powered by erty, Comenzi pentru setarea botului ***
n88=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** In general toate comenzile de bot incep cu /B, unde B vine de la BOT (ex: /BSEEN, /BPROTECT, etc). Pentru mai multe informatii despre o comanda tastati numele ei fara parametrii
n89=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /BPROTECT #canal [moduri] - modurile sunt optionale (daca lipsesc sunt afisate protectiile canaluli).
n90=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /BSEEN #canal [moduri] - modurile sunt optionale (daca lipsesc este afisat activitatea serviciului de !seen al respectivului canal).
n91=    }
n92=    else if ($1 == BSEEN) {
n93=      if ($2 == $null) {
n94=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: utilizare: /BSEEN <#channel> <activ/inactiv/on/off>
n95=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: <activ/inactiv> se refera la activitatea serviciului de !seen pe canalul <#channel>
n96=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: <on/off> daca va raspunde sau nu la comenzile de !seen pe canalul <#channel>
n97=        halt
n98=      }
n99=      if ($chr(35) isin $2) {
n100=        if ($3 == activ) {
n101=          writeini sn.txt $2 activ 1
n102=          sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: Serviciul de 4!seen5 a fost activat pentru canalul $2
n103=        }
n104=        else if ($3 == inactiv) {
n105=          writeini sn.txt $2 activ 0
n106=          sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: Serviciul de 4!seen5 a fost dezactivat pentru canalul $2
n107=        }
n108=        else if ($3 == on) {
n109=          writeini sn.txt $2 raspunde 1
n110=          sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: Serviciul de 4!seen5 a fost setat sa raspunda la comenzile de !seen pe canalul $2
n111=        }
n112=        else if ($3 == off) {
n113=          writeini sn.txt $2 raspunde 0
n114=          sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: Serviciul de 4!seen5 a fost setat sa NU raspunda la comenzile de !seen pe canalul $2
n115=        }
n116=        else if ($3 == $null) sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: Serviciul de 4!seen5 pentru canalul $2 este $iif($readini(sn.txt,$2,activ) == 1,activ,inactiv) si este setat sa $iif($readini(sn.txt,$2,raspunde) == 1,raspunda,nu raspunda) la comenzile de !seen
n117=        else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: 12- $3 -5Setare necunoscuta!
n118=      }
n119=      else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BSEEN: 12- $2 -5Nume de canal invalid!
n120=    }
n121=    else if ($1 == BPROTECT) {
n122=      if ($2 == $null) {
n123=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT -utilizare: /BPROTECT <#channel> [repeat/flood <<maxmsg> <maxtime>/bantime <time>/off>/autoban on/off]
n124=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT -sau: /BPROTECT <#channel> <invite/cheie/moderat/secret> <on/off>
n125=        halt
n126=      }
n127=      if ($chr(35) isin $2) {
n128=        if ($3 == $null) {
n129=          var %setting ""
n130=          if ($readini(prot.dat,$2,flood) == activa) %setting = %setting $chr(91) $+ FLOOD: maxim $readini(prot.dat,$2,maxflood) in $readini(prot.dat,$2,timeflood) secunde, TimeBan: $readini(prot.dat,$2,floodban) $+ $chr(93)
n131=          if ($readini(prot.dat,$2,repeat) == activa) %setting = %setting $chr(91) $+ REPEAT: maxim $readini(prot.dat,$2,maxrepeat) in $readini(prot.dat,$2,timerepeat) secunde, TimeBan: $readini(prot.dat,$2,repeatban) $+ $chr(93)
n132=          %setting = %setting $chr(91) $+ INVITE: $iif($readini(prot.dat,$2,invite) == 1,ON,OFF) $+ $chr(93)
n133=          %setting = %setting $chr(91) $+ CHEIE: $iif($readini(prot.dat,$2,key) == 1,ON,OFF) $+ $chr(93)
n134=          %setting = %setting $chr(91) $+ MODERAT: $iif($readini(prot.dat,$2,moderat) == 1,ON,OFF) $+ $chr(93)
n135=          %setting = %setting $chr(91) $+ SECRET: $iif($readini(prot.dat,$2,secret) == 1,ON,OFF) $+ $chr(93)
n136=          %setting = %setting $chr(91) $+ AUTOBAN: $iif($readini(prot.dat,$2,autoban) == 1,ON,OFF) $+ $chr(93)
n137=          sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Setarile de protectie pentru canalul $2 $+ : $iif(%setting == $null,OFF,%setting)
n138=          halt
n139=        }
n140=        if ($3 == repeat) {
n141=          if (($4 isnum 1-20) && ($5 isnum 1-60)) {
n142=            writeini prot.dat $2 repeat activa
n143=            writeini prot.dat $2 maxrepeat $4
n144=            writeini prot.dat $2 timerepeat $5
n145=            if ($readini(prot.dat,$2,repeatban) == $null) writeini prot.dat $2 repeatban 60
n146=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia de 4repeat pentru canalul $2 a fost activata pentru maxim $4 mesaje in $5 secunde
n147=          }
n148=          else if ($4 == off) {
n149=            writeini prot.dat $2 repeat inactiva
n150=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia de 4repeat pentru canalul $2 a fost dezactivata!
n151=          }
n152=          else if ($4 == bantime) {
n153=            if ($5 isnum 0-99999999) {
n154=              writeini prot.dat $2 repeatban $5
n155=              sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: TempBan pentru 4repeat pe canalul $2 a fost setat la $5 secunde
n156=            }
n157=            else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Timpul trebuie sa fie un numar pozitiv!
n158=          }
n159=          else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: 12- $4- -5Parametrii necunoscuti!
n160=        }
n161=        else if ($3 == flood) {
n162=          if (($4 isnum 1-20) && ($5 isnum 1-60)) {
n163=            writeini prot.dat $2 flood activa
n164=            writeini prot.dat $2 maxflood $4
n165=            writeini prot.dat $2 timeflood $5
n166=            if ($readini(prot.dat,$2,floodban) == $null) writeini prot.dat $2 floodban 60
n167=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia de 4flood pentru canalul $2 a fost activata pentru maxim $4 linii in $5 secunde
n168=          }
n169=          else if ($4 == off) {
n170=            writeini prot.dat $2 flood inactiva
n171=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia de 4flood pentru canalul $2 a fost dezactivata!
n172=          }
n173=          else if ($4 == bantime) {
n174=            if ($5 isnum 0-99999999) {
n175=              writeini prot.dat $2 floodban $5
n176=              sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: TempBan pentru 4flood pe canalul $2 a fost setat la $5 secunde
n177=            }
n178=            else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Timpul trebuie sa fie un numar pozitiv!
n179=          }
n180=          else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: 12- $4- -5Parametrii necunoscuti!
n181=        }
n182=        else if ($3 == invite) {
n183=          if ($4 == on) {
n184=            writeini prot.dat $2 invite 1
n185=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia impotriva setarii modului +i pe canalul $2 a fost activata
n186=          }
n187=          else if ($4 == off) {
n188=            writeini prot.dat $2 invite 0
n189=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia impotriva setarii modului +i pe canalul $2 a fost dezactivata
n190=          }
n191=          else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: 12- $4- -5Parametru necunoscut!
n192=        }
n193=        else if ($3 == cheie) {
n194=          if ($4 == on) {
n195=            writeini prot.dat $2 key 1
n196=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia impotriva setarii modului +k pe canalul $2 a fost activata
n197=          }
n198=          else if ($4 == off) {
n199=            writeini prot.dat $2 key 0
n200=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia impotriva setarii modului +k pe canalul $2 a fost dezactivata
n201=          }
n202=          else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: 12- $4- -5Parametru necunoscut!
n203=        }
n204=        else if ($3 == moderat) {
n205=          if ($4 == on) {
n206=            writeini prot.dat $2 moderat 1
n207=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia impotriva setarii modului +m pe canalul $2 a fost activata
n208=          }
n209=          else if ($4 == off) {
n210=            writeini prot.dat $2 moderat 0
n211=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia impotriva setarii modului +m pe canalul $2 a fost dezactivata
n212=          }
n213=          else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: 12- $4- -5Parametru necunoscut!
n214=        }
n215=        else if ($3 == secret) {
n216=          if ($4 == on) {
n217=            writeini prot.dat $2 secret 1
n218=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia impotriva setarii modului +s pe canalul $2 a fost activata
n219=          }
n220=          else if ($4 == off) {
n221=            writeini prot.dat $2 secret 0
n222=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Protectia impotriva setarii modului +s pe canalul $2 a fost dezactivata
n223=          }
n224=          else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: 12- $4- -5Parametru necunoscut!
n225=        }
n226=        else if ($3 == autoban) {
n227=          if ($4 == on) {
n228=            writeini prot.dat $2 autoban 1
n229=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Autoban pentru canalul $2 a fost activat
n230=          }
n231=          else if ($4 == off) {
n232=            writeini prot.dat $2 autoban 0
n233=            sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: Autoban pentru canalul $2 a fost dezactivat
n234=          }
n235=          else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: 12- $4- -5Parametru necunoscut!
n236=        }
n237=        else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: 12- $3 -5Setare necunoscuta!
n238=      }
n239=      else sockwrite -n bnc :psyBNC NOTICE %bncnick :/BPROTECT: 12- $2 -5Nume de canal invalid!
n240=    }
n241=    else if ($1 == BHELP) {
n242=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** psyBNC powered by erty, Lista de comenzi ***
n243=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /SETAWAYNICK nick       -Seteaza nick-ul de AWAY
n244=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /SETAWAYMSG  mesaj      -Seteaza mesajul de AWAY
n245=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /SETLEAVEMSG mesaj      -Seteaza mesajul care va fi scris cand plecati
n246=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /ADDAUTOOP mask :canale -Unde mask reprezinta un mask la o adresa, iar channels pot fi canale intr-o lista de genul #chan1,#chan2,... sau * (orice canal)
n247=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /DELAUTOOP mask         -Sterge adresa care se afla sub masca mask din autoOP list.
n248=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /LISTAUTOOP [mask]      -Listeaza toate adresele din autoOP.
n249=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /ADDPROT mask :canale   -Functioneaza la fel ca ADDAUTOOP, numai ca aceste adrese vor fi protejate impotriva modurilor: deop, ban, kick, de pe canalele specificate
n250=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /DELPROT mask           -Sterge adresa case se afla sub masca mask din autoprotect list.
n251=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /LISTPROT [mask]        -Listeaza toate adresele din autoprotect.
n252=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /PLAYPRIVATELOG         -Afiseaza logurile recente (citite sau necitite).
n253=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /ERASEPRIVATELOG        -Sterge toate logurile (citite sau necitite)
n254=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /NEWPASS <parola>       -Schimba parola de la psyBNC.
n255=      sockwrite -n bnc :psyBNC NOTICE %bncnick :*** /BCONNECT server:port   -se connecteaza la serverul respectiv
n256=    }
n257=    else if (($1 == SETAWAYMSG) && ($2 != $null)) {
n258=      writeini psyBNC.ini psyBNC awaymsg $2-
n259=      sockwrite -n bnc :psyBNC NOTICE %bncnick :Mesajul de AWAY a fost schimbat in " $+ $2- $+ "
n260=    }
n261=    else if (($1 == SETLEAVEMSG) && ($2 != $null)) {
n262=      writeini psyBNC.ini psyBNC leavemsg $2-
n263=      sockwrite -n bnc :psyBNC NOTICE %bncnick :Mesajul de AWAY a fost schimbat in " $+ $2- $+ "
n264=    }
n265=    else if (($1 == SETAWAYNICK) && ($2 != $null)) {
n266=      if ($left($2,1) !isnum) {
n267=        write -l1 defnick.dat $2
n268=        sockwrite -n bnc :psyBNC NOTICE %bncnick :AWAYNICK a fost schimbat in " $+ $2 $+ "
n269=      }
n270=      else sockwrite -n bnc :psyBNC NOTICE %bncnick :AWAYNICK invalid
n271=    }
n272=    else if ($1 == ADDAUTOOP) {
n273=      if ($2 == $null) {
n274=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/ADDAUTOOP - Utilizare: /ADDAUTOOP mask :canale, unde mask reprezinta un mask la o adresa, iar channels pot fi canale intr-o lista de genul #chan1,#chan2,... sau * (orice canal)
n275=        halt
n276=      }
n277=      if ($chr(58) !isin $3-) {
n278=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/ADDAUTOOP Parametrii invalizi (utilizare: /ADDAUTOOP mask :channels), unde mask reprezinta un mask la o adresa, iar channels pot fi canale intr-o lista de genul #chan1,#chan2,... sau * (orice canal)
n279=        halt
n280=      }
n281=      var %i 1
n282=      while (%i <= $ini(prot.dat,autoOP,0)) {
n283=        if ($ini(prot.dat,autoOP,%i) == $2) {
n284=          sockwrite -n bnc :psyBNC NOTICE %bncnick : $+ $2 se afla deja in autoOP List.
n285=          halt
n286=        }
n287=        inc %i
n288=      }
n289=      writeini prot.dat autoOP $2 $gettok($3-,1,58)
n290=      sockwrite -n bnc :psyBNC NOTICE %bncnick :Am adaugat12 $2 la autoOP (Canale: $gettok($3-,1,58) $+ )
n291=    }
n292=    else if (($1 == DELAUTOOP)) {
n293=      if ($2 == $null) {
n294=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/DELAUTOOP - Utilizare: /DELAUTOOP mask
n295=        halt
n296=      }
n297=      var %i 1
n298=      while (%i <= $ini(prot.dat,autoOP,0)) {
n299=        if ($ini(prot.dat,autoOP,%i) == $2) {
n300=          .remini prot.dat autoOP $ini(prot.dat,autoOP,%i)
n301=          sockwrite -n bnc :psyBNC NOTICE %bncnick :Am scos12 $ini(prot.dat,autoOP,%i) din autoOP
n302=          halt
n303=        }
n304=        inc %i
n305=      }
n306=      sockwrite -n bnc :psyBNC NOTICE %bncnick :12 $2 nu se afla in autoprotect.
n307=    }
n308=    else if ($1 == LISTAUTOOP) {
n309=      sockwrite -n bnc :psyBNC NOTICE %bncnick :AUTOOP LIST $iif($2,match on4 $2)
n310=      var %k 0
n311=      var %mch 0
n312=      var %i 1
n313=      while (%i <= $ini(prot.dat,autoOP,0)) {
n314=        if ($2) {
n315=          if ($testaddr($ini(prot.dat,autoOP,%i),$2)) {
n316=            var %mch 1
n317=            sockwrite -n bnc :psyBNC NOTICE %bncnick : $+ $ini(prot.dat,autoOP,%i) (Canale: $gettok($readini(prot.dat,autoOP,$ini(prot.dat,autoOP,%i)),1,58) $+ )
n318=          }
n319=        }
n320=        else {
n321=          var %mch 1
n322=          sockwrite -n bnc :psyBNC NOTICE %bncnick : $+ $ini(prot.dat,autoOP,%i) (Canale: $gettok($readini(prot.dat,autoOP,$ini(prot.dat,autoOP,%i)),1,58) $+ )
n323=        }
n324=        inc %i
n325=      }
n326=      if (%mch == 0) sockwrite -n bnc :psyBNC NOTICE %bncnick :Nu am gasit nimic
n327=    }
n328=    else if ($1 == ADDPROT) {
n329=      if ($2 == $null) {
n330=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/ADDPROT - Utilizare: /ADDPROT mask :channels, unde mask reprezinta un mask la o adresa, iar channels pot fi canale intr-o lista de genul #chan1,#chan2,... sau * (orice canal)
n331=        halt
n332=      }
n333=      if ($chr(58) !isin $3-) {
n334=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/ADDPROT - Parametrii invalizi (utilizare: /ADDPROT mask :canale), unde mask reprezinta o adresa, iar canalele pot fi reprezentate printr-o lista de genul #chan1,#chan2,..., sau * pentru orice canal
n335=        halt
n336=      }
n337=      var %i 1
n338=      while (%i <= $ini(prot.dat,autoprotect,0)) {
n339=        if ($ini(prot.dat,autoprotect,%i) == $2) {
n340=          sockwrite -n bnc :psyBNC NOTICE %bncnick :12 $2 se afla deja in autoprotect List.
n341=          halt
n342=        }
n343=        inc %i
n344=      }
n345=      writeini prot.dat autoprotect $2 $gettok($3-,1,58)
n346=      sockwrite -n bnc :psyBNC NOTICE %bncnick :Am adaugat12 $2 la autoprotect (Canale: $gettok($3-,1,58) $+ )
n347=    }
n348=    else if ($1 == DELPROT) {
n349=      if ($2 == $null) {
n350=        sockwrite -n bnc :psyBNC NOTICE %bncnick :/DELPROT - Utilizare: /DELPROT mask
n351=        halt
n352=      }
n353=      var %i 1
n354=      while (%i <= $ini(prot.dat,autoprotect,0)) {
n355=        if ($ini(prot.dat,autoprotect,%i) == $2) {
n356=          .remini prot.dat autoprotect $ini(prot.dat,autoprotect,%i)
n357=          sockwrite -n bnc :psyBNC NOTICE %bncnick :Am scos12 $ini(prot.dat,autoprotect,%i) din autoprotect
n358=          halt
n359=        }
n360=        inc %i
n361=      }
n362=      sockwrite -n bnc :psyBNC NOTICE %bncnick :12 $2 nu se afla in autoprotect.
n363=    }
n364=    else if ($1 == LISTPROT) {
n365=      sockwrite -n bnc :psyBNC NOTICE %bncnick :Protect List: $iif($2,match on4 $2)
n366=      var %k 0
n367=      var %i 1
n368=      while (%i <= $ini(prot.dat,autoprotect,0)) {
n369=        if ($2) {
n370=          if ($testaddr($ini(prot.dat,autoprotect,%i),$2)) sockwrite -n bnc :psyBNC NOTICE %bncnick : $+ $ini(prot.dat,autoprotect,%i) (Canale: $gettok($readini(prot.dat,autoprotect,$ini(prot.dat,autoprotect,%i)),1,58) $+ )
n371=        }
n372=        else sockwrite -n bnc :psyBNC NOTICE %bncnick : $+ $ini(prot.dat,autoprotect,%i) (Canale: $gettok($readini(prot.dat,autoprotect,$ini(prot.dat,autoprotect,%i)),1,58) $+ )
n373=        inc %i
n374=      }
n375=    }
n376=    else if ($1 == PLAYPRIVATELOG) {
n377=      var %pvlog 0
n378=      var %f 1
n379=      var %filenr $findfile(logs\,*,0)
n380=      while (%f <= %filenr) {
n381=        %find = $findfile(logs\,*,%f)
n382=        if (%pvlog == 0) var %pvlog 1
n383=        var %file $nopath(%find)
n384=        sockwrite -n bnc :psyBNC PRIVMSG %bncnick :1Mesaje scrise de $left(%file,-4)
n385=        var %i 1
n386=        while (%i <= $lines(logs\ $+ %file)) {
n387=          var %read $read -l $+ %i logs\ $+ %file
n388=          sockwrite -n bnc :psyBNC PRIVMSG %bncnick : $+ $right(%read,$calc(- $pos(%read,$chr(61),1)))
n389=          write -l $+ %i logs\ $+ %file read= $+ $right(%read,$calc(- $pos(%read,$chr(61),1)))
n390=          inc %i
n391=        }
n392=        sockwrite -n bnc :psyBNC PRIVMSG %bncnick :1Sfarsit mesaje ( $+ $left(%file,-4) $+ )
n393=        inc %f
n394=      }
n395=      if (%pvlog == 0) sockwrite -n bnc :psyBNC NOTICE %bncnick :Nu aveti nici un mesaj nou!
n396=    }
n397=    else if ($1 == ERASEPRIVATELOG) {
n398=      var %users ""
n399=      while ($findfile(logs\,*.*,0) != 0) {
n400=        if ($len(%users) >= 400) {
n401=          sockwrite -n bnc :psyBNC NOTICE %bncnick :Am sters logul de la user: %users
n402=          var %users ""
n403=        }
n404=        else %users = %users $left($nopath($findfile(logs\,*.*,1)),-4)
n405=        .remove " $+ $findfile(logs\,*.*,1) $+ "
n406=      }
n407=      if (%users) sockwrite -n bnc :psyBNC NOTICE %bncnick :Am sters logul de la user: %users
n408=    }
n409=    else if ($server != $null) {
n410=      if ($1 == MODE) {
n411=        if ($2 == %bncnick) {
n412=          if ($3 == +i) {
n413=            sockwrite -n bnc : $+ %bncnick MODE %bncnick : $+ +i
n414=            mode $me +i
n415=          }
n416=          else if ($3 == +w) {
n417=            mode $me +w
n418=            sockwrite -n bnc : $+ %bncnick MODE %bncnick : $+ +w
n419=          }
n420=          else if ($3 == +s) {
n421=            mode $me +s
n422=            sockwrite -n bnc : $+ %bncnick MODE %bncnick : $+ +s
n423=          }
n424=        }
n425=        else if ($chr(35) == $left($2,1)) {
n426=          if ($3 != $null) {
n427=            if (($me isop $2) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) $1-
n428=            else sockwrite -n bnc :psyBNC 482 %bncnick $2 :Nu esti operator pe canal!
n429=          }
n430=          else sockwrite -n bnc :psyBNC 324 %bncnick $2 $chan($2).mode $chan($2).topic
n431=        }
n432=      }
n433=      else if ($1 == PONG) {
n434=        set %PONG $ctime
n435=      }
n436=      else if (($1 == JOIN) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) {
n437=        if ($me !ison $2) join $2
n438=        else {
n439=          sockwrite -n bnc : $+ %fulladdress JOIN : $+ $2
n440=          .timer 1 1 bncjoin $2
n441=          .timer 1 1 mode $2
n442=          .timer 1 1 topic $2
n443=        }
n444=      }
n445=      else if (($1 == PART) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) {
n446=        if ($me ison $2) {
n447=          part $2-
n448=          sockwrite -n bnc : $+ %fulladdress PART : $+ $2
n449=        }
n450=      }
n451=      else if (($1 == TOPIC) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) {
n452=        if ($me isop $2) topic $2 $right($3-,-1)
n453=        else sockwrite -n bnc :psyBNC 482 %bncnick $2 :Nu esti operator pe canal!
n454=      }
n455=      else if (($1 == KICK) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) {
n456=        if ($me isop $2) {
n457=          if ($3 ison $2) kick $2 $right($3-,-1)
n458=          else sockwrite -n bnc :psyBNC 401 %bncnick $3 :Nick nu exista
n459=        }
n460=        else sockwrite -n bnc :psyBNC 482 %bncnick $2 :Nu esti operator pe canal!
n461=      }
n462=      else if ($1 == QUIT) {
n463=        .timerchecknick off
n464=        .timerpingpong off
n465=        .remini psyBNC.ini user
n466=        unset %BNCserver
n467=        nick $read -l1 defnick.dat
n468=        ame is AWAY ( $+ $readini(psyBNC.ini,psyBNC,leavemsg) $+ )
n469=        away $readini(psyBNC.ini,psyBNC,awaymsg)
n470=        sockwrite bnc ERROR :Closing Link: %bncnick $+ $chr(91) $+  %bncident $+ @ $+ %bncIP $+ $chr(93) (QUIT: $2-)
n471=        sockclose bnc
n472=      }
n473=      else if ($1 == BQUIT) {
n474=        unset %BNCserver
n475=        quit $2-
n476=        sockwrite -n bnc : $+ %fulladdress QUIT : $+ $2-
n477=      }
n478=      else if (($1 == WHOIS) && ($2 != $null)) whois $2
n479=      else if (($1 == WHO) && ($2 != $null)) who $2
n480=      else if ($1 == NOTICE) {
n481=        .raw NOTICE $2 $right($3-,-1)
n482=        if ($2 == %bncnick) sockwrite -n bnc : $+ %fulladdress NOTICE $2-
n483=      }
n484=      else if ($1 == PRIVMSG) {
n485=        if (($chr(35) isin $2) && ($me ison $2) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) .raw PRIVMSG $2 $3-
n486=        else .raw PRIVMSG $2 $3-
n487=      }
n488=    }
n489=  }
n490=  else {
n491=    sockwrite bnc ERROR :Closing Link: %bncnick $+ $chr(91) $+  %bncident $+ @ $+ %bncIP $+ $chr(93) (Invalid Protocol)
n492=    sockclose bnc
n493=  }
n494=}
n495=alias rj {
n496=  var %i 1
n497=  while (%i <= $chan(0)) {
n498=    if ($chan(%i) != $getchan($readini(defchan.ini,defchan,chan))) {
n499=      sockwrite -n bnc : $+ %fulladdress JOIN : $+ $chan(%i)
n500=      bncjoin $chan(%i)
n501=      mode $chan(%i)
n502=      topic $chan(%i)
n503=    }
n504=    inc %i
n505=  }
n506=}
n507=alias bncjoin {
n508=  var %nicks ""
n509=  var %i 1
n510=  while (%i <= $nick($1,0)) {
n511=    if ($len(%nicks) > 400) {
n512=      sockwrite -n bnc :psyBNC 353 %bncnick = $1 : $+ %nicks
n513=      %nicks = ""
n514=    }
n515=    else %nicks = %nicks $nick($1,%i).pnick
n516=    inc %i
n517=  }
n518=  if (%nicks) sockwrite -n bnc :psyBNC 353 %bncnick = $1 : $+ %nicks
n519=  sockwrite -n bnc :psyBNC 366 %bncnick $1 :Sfarsit Lista Nickuri.
n520=}
n521=alias checkbncnick {
n522=  if (%bncnick != $1) {
n523=    set %bncnick $1
n524=    set %fulladdress %bncnick $+ ! $+ %bncident $+ @ $+ %bncIP
n525=    if (%chnick != ON) sockwrite -n bnc : $+ %fulladdress NICK : $+ $1
n526=  }
n527=  unset %chnick
n528=  .timerchecknick 1 5 checkbncnick $me
n529=}
n530=alias pingpong {
n531=  if ($calc($ctime - %PONG) > 120) {
n532=    if ($server != $null) {
n533=      sockwrite bnc ERROR :Closing Link: %bncnick $+ $chr(91) $+  %bncident $+ @ $+ %bncIP $+ $chr(93) (Ping timeout)
n534=      .timerchecknick off
n535=      .remini psyBNC.ini user
n536=      unset %BNCserver
n537=      nick $read -l1 defnick.dat
n538=      ame is AWAY ( $+ $readini(psyBNC.ini,psyBNC,leavemsg) $+ )
n539=      away $readini(psyBNC.ini,psyBNC,awaymsg)
n540=    sockclose bnc }
n541=    else set %PONG $ctime
n542=  }
n543=  else sockwrite bnc PING :psyBNC
n544=}
n545=raw *:*:{
n546=  if (%servermsg == 0) {
n547=    if ($numeric == 376) {
n548=      write servermsg.ini $numeric $replace($2-,$chr(124),C124)
n549=      set %servermsg 1
n550=    }
n551=    else write servermsg.ini $numeric $replace($2-,$chr(124),C124)
n552=  }
n553=  if ($numeric == 800) write servermsg.ini $numeric $replace($2-,$chr(124),C124)
n554=  if (%BNCserver == ON) {
n555=    if (%servermsg == 0) {
n556=      if ($numeric == 376) {
n557=        sockwrite -n bnc :psyBNC $numeric %bncnick : $+ $2-
n558=        set %servermsg 1
n559=      }
n560=      else sockwrite -n bnc :psyBNC $numeric %bncnick : $+ $2-
n561=    }
n562=    if ($numeric == 800) sockwrite -n bnc :psyBNC $numeric %bncnick : $+ $2-
n563=    if ($numeric == 405) sockwrite -n bnc :psyBNC 405 %bncnick :Esti pe prea multe canale
n564=    else if (($numeric == 353) && ($3 != $getchan($readini(defchan.ini,defchan,chan)))) {
n565=      sockwrite -n bnc : $+ %fulladdress JOIN : $+ $3
n566=      sockwrite -n bnc :psyBNC 353 %bncnick $2-3 : $+ $4-
n567=    }
n568=    else if (($numeric == 366) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) sockwrite -n bnc :psyBNC 366 %bncnick $2 :Sfarsit lista nickuri
n569=    else if (($numeric == 367) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) sockwrite -n bnc :psyBNC 367 %bncnick $2-
n570=    else if ($numeric == 368) sockwrite -n bnc :psyBNC 368 %bncnick $2 :Sfarsit lista banuri
n571=    else if (($numeric == 329) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) sockwrite -n bnc :psyBNC 329 %bncnick $2-
n572=    else if (($numeric == 324) && ($2 != $getchan($readini(defchan.ini,defchan,chan)))) sockwrite -n bnc :psyBNC 324 %bncnick $2-
n573=    else if ($numeric == 332) sockwrite -n bnc :psyBNC 332 %bncnick $2 : $+ $3-
n574=    else if ($numeric == 333) sockwrite -n bnc :psyBNC 333 %bncnick $2-
n575=    else if ($numeric == 485) sockwrite -n bnc :psyBNC 485 %bncnick $2 :Nu esti proprietar de canal
n576=    else if ($numeric == 432) sockwrite -n bnc :psyBNC 432 %bncnick $2 :Nickname incorect
n577=    else if ($numeric == 421) sockwrite -n bnc :psyBNC 421 %bncnick $2 :Comanda necunoscuta
n578=    else if ($numeric == 473) sockwrite -n bnc :psyBNC 473 %bncnick $2 :Nu poti intra pe canal (invite)
n579=    else if ($numeric == 475) sockwrite -n bnc :psyBNC 475 %bncnick $2 :Nu poti intra pe canal (cheie)
n580=    else if ($numeric == 471) sockwrite -n bnc :psyBNC 471 %bncnick $2 :Nu poti intra pe canal (limita)
n581=    else if ($numeric == 404) sockwrite -n bnc :psyBNC 404 %bncnick $2 :Nu poti scrie pe canal (ban sau moderat)
n582=    else if ($numeric == 474) sockwrite -n bnc :psyBNC 474 %bncnick $2 :Nu poti intra pe canal (ban)
n583=    else if ($numeric == 441) sockwrite -n bnc :psyBNC 441 %bncnick $2 $3 :Nu se afla pe canal
n584=    else if ($numeric == 401) sockwrite -n bnc :psyBNC 401 %bncnick $2 :Canal sau nick inexistent
n585=    else if ($numeric == 311) sockwrite -n bnc :psyBNC 311 %bncnick $2 : $+ $3-
n586=    else if ($numeric == 433) sockwrite -n bnc :psyBNC 433 %bncnick $2 :Nick deja folosit
n587=    else if ($numeric == 319) sockwrite -n bnc :psyBNC 319 %bncnick $2 : $+ $3-
n588=    else if ($numeric == 312) sockwrite -n bnc :psyBNC 312 %bncnick $2 : $+ $3-
n589=    else if ($numeric == 313) sockwrite -n bnc :psyBNC 313 %bncnick $2 : $+ $3-
n590=    else if ($numeric == 320) sockwrite -n bnc :psyBNC 320 %bncnick $2 : $+ $3-
n591=    else if ($numeric == 317) sockwrite -n bnc :psyBNC 317 %bncnick $2 : $+ $3-
n592=    else if ($numeric == 318) sockwrite -n bnc :psyBNC 318 %bncnick $2 : $+ $3-
n593=    else if ($numeric == 352) sockwrite -n bnc :psyBNC 352 %bncnick $2 : $+ $3-
n594=    else if ($numeric == 315) sockwrite -n bnc :psyBNC 315 %bncnick $2 : $+ $3-
n595=    halt
n596=  }
n597=}
n598=alias timetype {
n599=  if (wk isin $1) return $left($1,$calc($pos($1,wk) - 1)) sapt
n600=  else if (days isin $1) return $left($1,$calc($pos($1,days) - 1)) zile
n601=  else if (day isin $1) return $left($1,$calc($pos($1,day) - 1)) zi
n602=  else if (hrs isin $1) return $left($1,$calc($pos($1,hrs) - 1)) ore
n603=  else if (hr isin $1) return $left($1,$calc($pos($1,hr) - 1)) ora
n604=  else if (mins isin $1) return $left($1,$calc($pos($1,mins) - 1)) minute
n605=  else if (min isin $1) return $left($1,$calc($pos($1,min) - 1)) minut
n606=  else if (secs isin $1) return $left($1,$calc($pos($1,secs) - 1)) secunde
n607=  else if (sec isin $1) return $left($1,$calc($pos($1,sec) - 1)) secunda
n608=}
n609=alias setromtime {
n610=  tokenize 32 $1-
n611=  unset %seentime
n612=  var %i 1
n613=  while (%i <= $0) {
n614=    if ($0 > 1) {
n615=      %seentime = %seentime $timetype($ [ $+ [ $calc(%i - 1) ] ])
n616=    }
n617=    else %seentime = %seentime $timetype($ [ $+ [ %i ] ])
n618=    inc %i
n619=  }
n620=  return %seentime
n621=}
n622=on 1:connect:if (%BNCserver == ON) sockwrite -n bnc :psyBNC 372 %bncnick :M-am conectat la server-ul $server $+ : $+ $port
n623=on 1:nick:{
n624=  if (%BNCserver == ON) {
n625=    if ($newnick == %chnick) {
n626=      set %bncnick $newnick
n627=      set %fulladdress %bncnick $+ ! $+ %bncident $+ @ $+ %bncip
n628=      sockwrite -n bnc : $+ %fulladdress NICK : $+ $newnick
n629=    }
n630=    else sockwrite -n bnc : $+ $fulladdress NICK : $+ $newnick
n631=  }
n632=  unset %chnick
n633=}
n634=alias breaktime {
n635=}
n636=on 1:notice:*:*:if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress NOTICE $target : $+ $1-
n637=on 1:text:*:*:if ((%BNCserver == ON) && ($target != $getchan($readini(defchan.ini,defchan,chan))) && ($1 != login) && ($1 != !defchan)) sockwrite -n bnc : $+ $fulladdress PRIVMSG $target : $+ $1-
n638=on 1:DISCONNECT:{
n639=  set %servermsg 0
n640=  write -c servermsg.ini
n641=  if (%BNCserver == ON) sockwrite -n bnc :psyBNC 372 %bncnick :M-am deconectat de la server $server $+ : $+ $port
n642=}
n643=on 1:MODE:#:if ((%BNCserver == ON) && ($target != $getchan($readini(defchan.ini,defchan,chan)))) sockwrite -n bnc : $+ $fulladdress MODE $chan $1-
n644=on 1:OP:#:{
n645=  if (%lastop != $nick $+ $target $+ $1-) {
n646=    set %lastop $nick $+ $target $+ $1-
n647=    if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress MODE $chan $1-
n648=  }
n649=}
n650=on 1:DEOP:#:{
n651=  if (%lastdeop != $nick $+ $target $+ $1-) {
n652=    set %lastdeop $nick $+ $target $+ $1-
n653=    if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress MODE $chan $1-
n654=  }
n655=}
n656=on 1:VOICE:#:{
n657=  if (%lastvoice != $nick $+ $target $+ $1-) {
n658=    set %lastvoice $nick $+ $target $+ $1-
n659=    if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress MODE $chan $1-
n660=  }
n661=}
n662=on 1:DEVOICE:#:{
n663=  if (%lastdevoice != $nick $+ $target $+ $1-) {
n664=    set %lastdevoice $nick $+ $target $+ $1-
n665=    if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress MODE $chan $1-
n666=  }
n667=}
n668=on 1:BAN:#:if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress MODE $chan $1-
n669=on 1:UNBAN:#:if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress MODE $chan $1-
n670=on 1:KICK:#:if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress KICK $chan $knick : $+ $1-
n671=on 1:TOPIC:#:if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress TOPIC $chan : $+ $1-
n672=on 1:QUIT:if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress QUIT : $+ $1-
n673=on 1:JOIN:#:if ((%BNCserver == ON) && ($chan != $getchan($readini(defchan.ini,defchan,chan)))) sockwrite -n bnc : $+ $fulladdress JOIN : $+ $chan
n674=on 1:PART:#:if ((%BNCserver == ON) && ($target != $getchan($readini(defchan.ini,defchan,chan)))) sockwrite -n bnc : $+ $fulladdress PART : $+ $chan
n675=on 1:CTCPREPLY:*:if (%BNCserver == ON) sockwrite -n bnc : $+ $fulladdress NOTICE %bncnick : $+ $1- $+ 
n676=on 1:ACTION:*:*:if ((%BNCserver == ON) && ($target != $getchan($readini(defchan.ini,defchan,chan))) && ($1 != login) && ($1 != !defchan)) sockwrite -n bnc : $+ $fulladdress PRIVMSG $target :ACTION $1- $+ 
