   [aliases]
   n0=;xxxxxxxxxxxxxxxxxxxxxxx
   n1=; extreme v8.21
   n2=; channel aliases section (mostly)
   n3=;xxxxxxxxxxxxxxxxxxxxxxx
   n4=;
   n5=unbanall bresetall #
   n6=unbanmine bresetmine #
   n7=bresetall if ($1) { set $v%(resetall.,$1) 1 | mode $1 +b }
   n8=bresetmine if ($1) { set $v%(resetmine.,$1) 1 | mode $1 +b }
   n9=bans if (# ischan) { if ($window($+(@Bans ,#))) window -c $+(@Bans ,#) | if ($chan(#).ibl == $true) && ($me isop #) && ($ibl(#,1)) _bans2 | else mode # +b }
   n10=_bans2 {
   n11=  var %n = 1
   n12=  while (%n <= $ibl(#,0)) {
   n13=    var %b = $ibl(#,%n)
   n14=    if (%b iswm %§.myaddress) || (%b iswm $puttok(%§.myaddress,$ip,2,64)) var %banned = $addtok(%banned,%b,32)
   n15=    _addban # %b $+ $chr(9) $+ $_c(4) $+ $iif($ibl(#,%n).by,$remove($ibl(#,%n).by,$chr(9),$chr(40),$chr(41)),-) $+  $+ $chr(9) $+ $validbandur($calc($ctime - $ibl(#,%n).ctime)) ago
   n16=    inc %n
   n17=  }
   n18=  if ($me isop #) && (%banned) _rmmybans # %banned
   n19=  window -rb $+(@Bans ,#)
   n20=}
   n21=_unb if ($me isop $1) { .raw mode $1 -b $2 | _--unset tempbans $+($1,:,$2) }
   n22=_bbb if ($me isop #) && (($ibl(#,1)) || ($chan(#).ibl != $true)) return $1-
   n23=_bbb2 if ($chan(#).ibl != $true) || ($ibl(#,1)) return view bans $+ $chr(9) $+ $iif($chan(#).ibl,$+($chr(40),$ibl(#,0),$chr(41)))
   n24=_bbb3 if ($chan(#).ibl != $true) || ($ibl(#,1)) return unban $+ $chr(9) $+ ( $+ $ibl(#,0) $+ )
   n25=_bbb4 if ($_v%(lastunbans.,#)) return reban $+ $chr(9) $+ ( $+ $numtok($_v%(lastunbans.,#),44) $+ )
   n26=_fdeopban var %n = 1 | while ($nick($1,%n,o)) { var %u = $ifmatch | if ($2 iswm $address(%u,5)) && (%u != $me) var %l = $addtok(%l,%u,32) | if ($numtok(%l,32) == %§.nets-max.modes) { .raw MODE $1 - $+ $str(o,$numtok(%l,32)) %l | unset %l } | inc %n } | if (%l) { if ($3) || ($hget(x_prot,kickops)) .raw MODE $1 - $+ $str(o,$numtok(%l,32)) $+ +b %l $2 | else halt } | else .raw MODE $1 +b $2
   n27=_addban if (!$window($+(@Bans ,$1))) { var %w = $+(@Bans ,$1) | window -klbn -t40,70 %w $_gcoord(bans,-1 -1 620 250) @Ban $_@font | if ($me isop $1) titlebar %w (double-click to unban) | aline %w $_nchr(%w) | aline %w $_nchr(%w) | aline %w $_c(1) $+ Banmask $+ $chr(9) $+ $_c(1) $+ Banned by $+ $chr(9) $+ $_c(1) $+ When | aline %w $_nchr(%w) } | aline $+(@Bans ,$1) $2-
   n28=_arbans var %n = 1 | while (%n <= $numtok($3-,32)) { var %b = %b $gettok($3-,%n,32) | if (4 // %n) { .raw mode $2 $1 $+ bbbb %b | unset %b } | inc %n } | if (%b) .raw mode $2 $1 $+ $str(b,$numtok(%b,32)) %b
   n29=_rmmybans if ($input(There are ban(s) in $1 that include you. $+ $crlf $+ Remove them?,8,Bans)) .raw MODE $1 - $+ $str(b,$numtok($2-,32)) $2-
   n30=banstoreset set %§.rbans $addtok(%§.rbans,$2,32) | if ($numtok(%§.rbans,32) == 4) { .raw mode $1 -bbbb %§.rbans | unset %§.rbans } | halt
   n31=b {
   n32=  if ($isid) return $remove($left($1-,940),$chr(32))
   n33=  if ($away) && ($1 == $null) { back | return }
   n34=  if ($me !isop #) || (!$1) return
   n35=  elseif ($1 ischan) ban $1-
   n36=  elseif (*!*@* iswm $1) {
   n37=    if ($gettok($1-,-1,32) isnum) var %t = $ifmatch, %bl = $deltok($1-,-1,32)
   n38=    else var %bl = $1-
   n39=    mode # + $+ $str(b,$numtok(%bl,32)) %bl
   n40=    if (%t) .timer 1 %t mode # - $+ $str(b,$numtok(%bl,32)) %bl
   n41=  }
   n42=  else {
   n43=    var %tp = $iif($2 isnum 0-15,$2,$iif($hget(x_prot,defban) isnum 0-15,$hget(x_prot,defban),3))
   n44=    if ($3 isnum) var %t = $3
   n45=    ban $iif(%t,-u $+ %t) # $nc($1).chan %tp
   n46=  }
   n47=  if (%t) ale Ban(s) will be auto-removed in %t secs.
   n48=}
   n49=
   n50=unban if ($1 == *) || ($1 == *!*@*) || ($1 == all) bresetall | elseif ($_v%(lastban.,#)) mode # -b $_v%(lastban.,#)
   n51=reban if ($_v%(lastunbans.,#)) mode # +b $gettok($_v%(lastunbans.,#),1,44)
   n52=banpart if ($_v%(part.mask.,#)) mode # +b $_mask($ifmatch,$hget(x_prot,defban))
   n53=bankick if ($_v%(kick.mask.,#)) mode # +b $_mask($ifmatch,$hget(x_prot,defban))
   n54=
   n55=mode if (!$2) set $v%(mode.,$1) 1 | .raw MODE $1-
   n56=beep if ($1) { if ($1 == on) { _unset sets beep | ale Beeps are now ON } | elseif ($1 == off) { _set sets beep 1 | ale Beeps are now OFF } | elseif (!$hget(x_sets,beep)) && ($1 isnum) && (($away == $false) || ($hget(x_away,noflash) != 1)) beep $1- } | else sr Parameters missing.
   n57=flash {
   n58=  if ($hget(x_sets,noflashing)) || (($away) && ($hget(x_away,noflash))) return
   n59=  if (-*r* iswm $1) flash $1-
   n60=  else {
   n61=    if (-* iswm $1) var %s = $remove($1,-), %l = $2- | else var %l = $1-
   n62=    if ($away) flash -r $+ $iif($hget(x_sets,flashtimesaway),$ifmatch,2) $+ %s %l
   n63=    else flash -r $+ $iif($hget(x_sets,flashtimes),$ifmatch,4) $+ %s %l
   n64=  }
   n65=}
   n66=bflash if ($flash.onquery) flash ( $+ $1 $+ ) | beep 3 100
   n67=ctcpreply echo $colour(ctcp) -qa $ech(myctcpreply,$1,$2,$3-) | .raw NOTICE $1 : $+ $upper($2) $3- $+  | halt
   n68=c {
   n69=  if ($numtok($1,44) == 1) && ($1 !ischan) set -u120 $v%(fping.,$1) $calc($ticks / 1000)
   n70=  elseif (($nick($1,0,a) > 1) || ($numtok($1,44) > 3)) && ($hget(x_sets,noreplywindow) != 1) {
   n71=    var %w = @Replies, %i = 1
   n72=    window -c %w
   n73=    window -knl -t20 %w $_gcoord(replies,-1 -1 300 200) $_@font
   n74=    titlebar %w for $upper($2) in $iif($1 !ischan,selected nicks,$1)
   n75=    aline %w $_nchr(%w)
   n76=    aline %w $_c(1) $+ Nick $+ $chr(9) $+ $_c(1) $+ Reply
   n77=    aline %w $_nchr(%w)
   n78=    if ($1 ischan) {
   n79=      while ($nick($1,%i,a)) { var %ni = $ifmatch | if (%ni != $me) aline %w %ni $+ $chr(9) $+ Waiting for reply | inc %i }
   n80=    }
   n81=    else {
   n82=      while ($gettok($1,%i,44)) { var %ni = $ifmatch | if (%ni != $me) aline %w %ni $+ $chr(9) $+ Waiting for reply | inc %i }
   n83=      aline %w $_nchr(%w)
   n84=    }
   n85=  }
   n86=  mstuff $1 ctcp §£ $2-
   n87=  if ($1 ischan) set -u120 %§.lastctcp $1
   n88=}
   n89=ctcp {
   n90=  if ($isid) {
   n91=    tokenize 32 $1 | var %1 = $upper($1)
   n92=    if ($1 == sound) return %1 ( $+ $replace($iif($2 != $null,$2-,n/a),_,$chr(32),$+($chr(37),20),$chr(32)) $+ )
   n93=    if ($2 == send) return %1 $2 ( $+ $3 $+ ) size: $iif($6 isnum,$bytes($6,3).suf,n/a)
   n94=    if ($2 == chat) return %1 $2
   n95=    return %1 $iif(($2 && $2 !isnum),$chr(40) $+ $2- $+ $chr(41))
   n96=  }
   n97=  if (!$server) || (!$2) return | var %n = $1
   n98=  if ($2 == sound) { if ($3) .raw PRIVMSG %n :SOUND $gettok($3-,-1,92) $+  | else return }
   n99=  elseif ($2 == ping) .raw PRIVMSG %n :PING $iif($3,$3-,$ticks) $+
   n100=  else { if ($3) .raw PRIVMSG %n : $+ $upper($2) $3- $+  | else .raw PRIVMSG %n : $+ $upper($2) $+  }
   n101=  echo $colour(ctcp) -qa $display(myctcp,%n,$upper($2) $3-)
   n102=}
   n103=noreplywindow if ($input(Show ctcp replies in active window?,8,CTCP)) _unset sets noreplywindow | else _set sets noreplywindow 1
   n104=j join $1-
   n105=joinall {
   n106=  var %l = $iif($hget($+(x_net-,%§.network),chan.def),$hget(x_net-default,chan.list),$hget($+(x_net-,%§.network),chan.list)), %i = 1, %aj = $_n(chan.ajoindelay)
   n107=  if (!%l) { nets 3 | return }
   n108=  while ($gettok(%l,%i,44)) {
   n109=    var %c = $ifmatch
   n110=    if (%aj) && ($1) { if ($gettok(%c,1,32) !ischan) .timerjoindelay. $+ %i 1 $calc(%aj * (%i -1)) join $iif(%i != 1,-n) %c }
   n111=    elseif ($numtok(%c,32) > 1) {
   n112=      if ($gettok(%c,-1,32) == +i) {
   n113=        set -u30 %§.invite.req $addtok(%§.invite.req,$gettok(%c,1,32),44)
   n114=        if (%§.s^c) cs invite $gettok(%c,1,32) $me
   n115=        elseif (%§.network == undernet) msg.x invite $gettok(%c,1,32) $me
   n116=      }
   n117=      else join %c
   n118=    }
   n119=    else var %d = $addtok(%d,%c,44)
   n120=    if ($numtok(%d,44) == 4) { join %d | unset %d }
   n121=    inc %i
   n122=  }
   n123=  if (%d) join %d
   n124=}
   n125=join if (!$server) halt | if (!$1) { joinall | return } | if (-* iswm $1) join $1 $iif($2,$_chan($2-)) | elseif ($chr(44) isin $1) join $1- | else join $_chan($1-)
   n126=cycle var %c = $iif($1 ischan,$1,#), %m = $iif($1 ischan,$2-,$1-) | if (%c !ischan) return | j %c | if (i isin $chan(%c).mode) && ($istok(ircnet,%§.network,46) == $false) && ($nick(%c,0) > 1) { ale You can't cycle channels with +i mode. | return } | set $v%(cycled.,%c) 1 | hop %m
   n127=cycall var %i = $chan(0), %c | while (%i) { %c = $chan(%i) | if (i !isin $gettok($chan(%i).mode,1,32)) || ($nick(%c,0) == 1) { hop -c %c | set $v%(cycled.,%c) 1 } | else echo %c $logo You can't cycle %c $+ , it's invite only. | dec %i }
   n128=rejoin cycle
   n129=m msg $1-
   n130=p if ($1 ischan) || ((!$1) && (#)) part $iif($1,$1-,#) | elseif ($1) ping $1
   n131=part if (!$server) return | if ($1 ischan) .raw part $1 $iif($2,: $+ $2-) | elseif (# ischan) .raw part # $iif($1,: $+ $1-) | elseif ($chan(0)) partall
   n132=i if ($2 ischan) invite $1- | else ignore $1-
   n133=ign var %n = $iif($1,$iif(@ isin $1,$1,$iif($address($1,2),$ifmatch,$+($1,!*@*))),$remove($active,=)), %t = $iif($2 isnum,$2) | ignore -pntikd $+ $iif(%t,$+(u,%t)) %n | if (!%t) set %§.clearthis $addtok(%§.clearthis,%n,32) | if ($query($1)) close -m $1
   n134=invite var %n = $nc($1), %c = $iif($_chan($2) ischan,$_chan($2),#) | if (%c !ischan) ae $_error Usage: /invite <nick> [#channel] | elseif (%n) && (%c) { invite %n %c | ae $ech(myinvite,%n,%c) }
   n135=cmode if (#) && ($1) .mode # $1-
   n136=umode if ($1) .raw mode $me $1-
   n137=motd if ($server) { set %§.motd 1 | .raw motd $1- }
   n138=rkick if ($active !ischan) || ($nick(#,0,a) == 1) return | :n | var %n = $nick(#,$rand(1,$nick(#,0))) | if (%n == $me) goto n | kick # %n random kick
   n139=rk rkick $1-
   n140=ks if ($active ischan) && ($snick($active,1)) qkick $active $snick($active,1) $rkmsg
   n141=ksel ks
   n142=_botkick if ($input(Use $2 to $iif($3,kickban,kick) $1 $+ ?,8,Bot)) .msg $2 $iif($3,kickban,kick) $1 $_pwd($hget(x_bots,$2))
   n143=tempkb tkb $1-
   n144=tkb var %t = $inp(Time for ban in minutes~Tempban~10,1), %1 = $nc($1,1).chan, %tb = $_address(%1,$hget(x_prot,defban)), %t2 = $calc(%t *60) | .raw MODE # $iif(%1 isop #,-o+b %1,+b) %tb $+ $lf $+ KICK # %1 : $+ $iif($2,$2-,$rkmsg) ... $+ %t $+  $+ $iif(%t > 1,mins,min) tempban $_nkicks | .timerunb $+ $rand(1,99) 1 %t2 _unb # %tb
   n145=kaway set $v%(kaway.,#) 1 | .raw who #
   n146=kbaway set $v%(kaway.,#) -b | .raw who #
   n147=k kick $1 $2-
   n148=kick if (* isin $1) || (? isin $1) fk $1- | else _kstuff -k $1-
   n149=kb if (* isin $1) || (? isin $1) fkb $1- | else _kstuff -kb $1 $2-
   n150=bl _kbl # $1 b
   n151=kl _kbl # $1
   n152=kbl _kbl # $1 kb
   n153=_kbl if ($_v%(lastjoin.,$1,.ips)) { var %n = 1, %n2 = $numtok($_v%(lastjoin.,$1,.ips),44), %l = $iif(($2 isnum && $2 <= %n2),$2,%n2) | while (%n <= %l) { $iif($3 == b,b,$iif($3,fkb,fk)) $gettok($_v%(lastjoin.,$1,.ips),%n,44) | inc %n } }
   n154=_kstuff if (!$2) return | var %n = $nc($iif($2 ischan,$3,$2),1), %c = $iif($2 ischan,$2,#), %m = $iif($2 ischan,$4-,$3-) | if (%c) && (%n) { if ($me !isop %c) && ($isthereabot(%c)) _botkick %n $ifmatch $iif($1 == -kb,1) | else _kstuff2 $1 %c %n %m }
   n155=_kstuff2 if ($3 ison $2) { var %1 = KICK $2 $3 : $+ $iif($4,$4-,$rkmsg) $_nkicks | if ($1 == -kb) .raw MODE $2 $iif($3 isop $2,-o+b $3,+b) $_address($3,$hget(x_prot,defban)) $+ $lf $+ %1 | else .raw %1 } | else ale $_c(3) $+ $3 is not on $2
   n156=qkick if ($2 ison $1) .raw KICK $1-2 : $+ $3- $_nkicks
   n157=qkickb if ($2 ison $1) { .raw MODE $1 $iif($2 isop $1,-o+b $2,+b) $_address($2,$hget(x_prot,defban)) $+ $lf $+ KICK $1-2 : $+ $3- $_nkicks }
   n158=qtkickb if ($2 ison $1) { var %tb = $_address($2,$hget(x_prot,defban)) | .raw MODE $1 $iif($2 isop $1,-o+b $2,+b) %tb $+ $lf $+ KICK $1-2 : $+ $3- ...tempban for $hget(x_prot,tempban) $+  $+ $iif($hget(x_prot,tempban) == 1,min,mins) $_nkicks | var %time = $calc($hget(x_prot,tempban) *60) | .timer 1 %time _unb $1 %tb | hadd -m x_tempbans $+($1,:,%tb) $calc($ctime + %time) }
   n159=qnkickb .raw MODE $1 $iif($2 isop $1,-o+b $2,+b) $2 $+ !*@* $+ $lf $+ KICK $1-2 : $+ $3- $_nkicks | .timer 1 $calc($hget(x_prot,tempban) *60) .raw mode $1 -b $2 $+ !*@*
   n160=guestkickban fkb $iif($1 ischan,$1,#) guest*!*@* $_km($iif($1 ischan,$1,#),guests)
   n161=op _odp + $1-
   n162=deop _odp - $1-
   n163=voice _odv + v $1-
   n164=devoice _odv - v $1-
   n165=vo _odv + v $1-
   n166=dvo _odv - v $1-
   n167=own _odv + q $1-
   n168=deown _odv - q $1-
   n169=_odv mode # $1 $+ $str($2,$numtok($3-,32)) $nc($3,1).chan $4-
   n170=_odp var %n = $nc($2,1).chan | if (%n !ison #) return | if ($me !isop #) && ($isthereabot(#)) .msg $ifmatch $iif($1 == +,op,deop) %n $_pwd($hget(x_bots,$ifmatch)) | else mode # $1 $+ $str(o,$numtok($2-,32)) %n $3-
   n171=deopme if ($me isop #) deop $me
   n172=opme if (%§.s^n) cs op # $me | else op $me
   n173=chopdeop var %n = $numtok($2-,44) | while (%n) { var %c = $gettok($2-,%n,44) | cs $iif(%c isop $1,deop,op) $1 %c | dec %n }
   n174=changemode {
   n175=  var %c = $numtok($3-,44)
   n176=  while (%c) {
   n177=    var %n = $gettok($3-,%c,44)
   n178=    if ($_nomode($1,$2,%n)) {
   n179=      var %+ = $addtok(%+,%n,32)
   n180=      if ($numtok(%+,32) == %§.nets-max.modes) { .raw mode $1 + $+ $str($2,%§.nets-max.modes) %+ | unset %+ }
   n181=    }
   n182=    else {
   n183=      var %- = $addtok(%-,%n,32)
   n184=      if ($numtok(%-,32) == %§.nets-max.modes) { .raw mode $1 - $+ $str($2,%§.nets-max.modes) %- | unset %- }
   n185=    }
   n186=    dec %c
   n187=  }
   n188=  if (%+) .raw mode $1 + $+ $str($2,$numtok(%+,32)) %+
   n189=  if (%-) .raw mode $1 - $+ $str($2,$numtok(%-,32)) %-
   n190=}
   n191=w wi $1-
   n192=whois if ($1) whois $1-
   n193=wi whois $iif($1,$1,$me) $iif(!$ircx,$iif($1,$1,$me))
   n194=ww whowas $1-
   n195=getidle if ($1) { set $v%(idle.,$1) 1 | .raw whois $1 $1 }
   n196=getip if ($1) { set $v%(getip.,$1) 1 | .raw userhost $1 }
   n197=uhost getip $1
   n198=userhost getip $1
   n199=ping c $iif($1,$1,$remove($active,=)) ping
   n200=page if ($2) c $1 page $2- | else c $1 page $inp(What msg?~Pager,1)
   n201=version c $iif($1,$1,$remove($active,=)) version
   n202=ver if ($1) version $1-
   n203=s server $1-
   n204=chat if (!$1) return | if ($lock(chat)) ale Private chats are locked. | else dcc chat $1-
   n205=send if (!$1) return | if ($lock(send)) ale DCC Sends are locked. | else dcc send $1-
   n206=dccs ale Active DCC's: Sends: $send(0) - Gets: $get(0) - Chats: $chat(0) - FServs: $fserv(0) (Total: $calc($send(0) + $get(0) + $chat(0) + $fserv(0)) $+ )
   n207=adcc if ($hget(x_ctcps,dcc) == 1) { set -u300 $v%(adcc.,$1) 1 | if ($show) ale $1 is allowed to DCC you for the next 5 minutes. } | else sr This only makes sense if you turn DCC's off in the main setup
   n208=
   n209=ce {
   n210=  if (!$1) return
   n211=  var %w = $gettok($1,1,40), %i = $hget(x_wins,%w), %cl = $colour($iif(services !isin $1,$1,notice))
   n212=  if (%i == 2) ce2 $1-
   n213=  elseif (%i == 1) echo %cl -sm $2-
   n214=  else echo %cl -a $2-
   n215=}
   n216=ce2 {
   n217=  var %w = $+(@,$1), %col = $colour($iif(services* !iswm $1,$1,notice))
   n218=  ce.window $1
   n219=  if (@services isin %w) {
   n220=    var %nick = $lower($right($left($remove($1,services),-1),-1))
   n221=    if (%§.nets-log.msgserv) editbox -p %w /msg %nick
   n222=    else editbox -p %w / $+ %nick
   n223=  }
   n224=  elseif ($1 != snotices) && (($1 != wallops) || (o isin $usermode)) editbox -p %w / $+ $iif($1 == notices,notice $iif($target != $me,$target,$me)),$lower($1))
   n225=  aline -p %col %w $2-
   n226=  if ($hget(x_sets,$+(save.tome.,%w))) write $+(logs\,%w,.txt) $2-
   n227=  window -g1 %w
   n228=}
   n229=ce3 {
   n230=  if ($hget(x_wins,$1)) ce2 $1-
   n231=  else {
   n232=    var %a = $iif($1 == whois,$whoisonact,$ctcponact), %cl = $colour($iif(services !isin $1,$1,notice))
   n233=    if (%a) echo %cl -a $2-
   n234=    else echo %cl -sm $2-
   n235=  }
   n236=}
   n237=ce.window {
   n238=  var %w = $+(@,$remove($1,@)), %f = $+(logs\,%w,.txt)
   n239=  if (!$window(%w)) {
   n240=    window -eka $+ $iif($2 == $null,n) +e %w $_gcoord($1,150 100 550 250) %w $_@font
   n241=    if (($2) || ($hget(x_sets,$+(save.tome.load.,%w)))) && ($isfile(%f)) {
   n242=      loadbuf %w %f
   n243=      linesep %w
   n244=    }
   n245=  }
   n246=}
   n247=_echwhois {
   n248=  if ($_v%(whois.on.query.,$1)) && ($query($1)) { var %query = 1 | linesep $1 }
   n249=  var %h = x_tmp-whois. $+ $1
   n250=  if ($2- == end) {
   n251=    var %n = 1
   n252=    while ($hget(%h,%n)) {
   n253=      var %l = $ifmatch
   n254=      if (%query) echo $colour(whois) $1 %l
   n255=      else ce3 Whois %l
   n256=      inc %n
   n257=    }
   n258=    hfree %h
   n259=    unset $v%(whois.on.query.,$1)
   n260=    if (%query) linesep $1
   n261=  }
   n262=  else hadd -m %h $calc($hget(%h,0).item +1) $2-
   n263=}
   n264=fakeping if ($1 isnum) _set ctcps fakeping $1 | elseif ($1 == off) { _unset ctcps fakeping | ale Fake ping reply is now off | return } | if ($hget(x_ctcps,fakeping)) ale Fake ping reply set to $ifmatch secs | else ale Fake ping not set yet.
   n265=ftp if ($script(taboftp.mrc)) cftp $1- | else { if ($isfile(addons\taboftp\taboftp.mrc)) { sr -t Info FTP Client (by tabo) | .load -rs addons\taboftp\taboftp.mrc | cftp $1- } }
   n266=topic if (# ischan) { if ($1) .raw TOPIC # : $+ $1- | else .raw TOPIC # }
   n267=say {
   n268=  var %target = $iif($event == input,$target,$iif($validwin($1),$1,$active)), %text = $iif(($event == input || %target != $1),$1-,$2-)
   n269=  if ($validwin(%target) != 1) || (!%text) return
   n270=  if ($istok(rot13.rot127.enc,%§.encrypt,46)) && (=* !iswm %1) {
   n271=    %§.encrypt %target %text
   n272=    var %enc = ( $+ $lower(%§.encrypt) $+ )
   n273=  }
   n274=  else .msg %target %text
   n275=  if ((%target ischan) || ($query(%target)) || ($chat($remove(%target,=)))) && ($show) {
   n276=    var %display = $iif($prefixownmsgs,$display(say,$_align($_ns($me,%target)),%text),> %text)
   n277=    echo $colour(own) -qti2 %target %display %enc
   n278=  }
   n279=  if ($_v%(redir.,%target)) .timer 1 0 say $ifmatch $strip(%display,c)
   n280=}
   n281=msg {
   n282=  if ($2 != $null) && (($server) || (=* iswm $1)) {
   n283=    .msg $1-
   n284=    if ($show) {
   n285=      if ($active != $1) echo $colour(own) $iif(@* iswm $active,-si2,-ai2) $display(msg,$1,$2-)
   n286=      if (($query($1)) || ($chat($remove($1,=))) || ($1 ischan)) && ($event != input) echo $colour(own) -t $1 $iif($prefixownmsgs,$display(say,$_align($_ns($me,$1)),$2-),> $2-)
   n287=    }
   n288=  }
   n289=}
   n290=q query $nc($1) $2-
   n291=query {
   n292=  if (-* iswm $1) var %s = $1, %n = $2, %t = $3- | else var %n = $1, %t = $2-
   n293=  if (%n) {
   n294=    if (!$query(%n)) {
   n295=      query %s %n
   n296=      if ($query(%n)) {
   n297=        var %file = $shortfn($logdir) $+ $mklogfn(%n)
   n298=        if ($hget(x_display,loadbuf.query)) && ($isfile(%file)) loadbuf %n %file
   n299=        else echo %n Query with %n ( $+ $iif($address(%n,5),$gettok($ifmatch,2,33),<unknown>) $+ )
   n300=        echo %n Common channels: $_comchans(%n).pnick
   n301=        linesep %n
   n302=      }
   n303=    }
   n304=    if (%t) {
   n305=      .msg %n %t
   n306=      echo $colour(own) -mti2 %n $iif($prefixownmsgs,$display(say,$_align($me),%t),> %t)
   n307=    }
   n308=  }
   n309=}
   n310=describe if (!$1) || ((!$server) && (=* !iswm $1)) return | .describe $1- | var %disp = $display(me,$_ns($me,$1),$2-) | echo $colour(own) -qti2 $1 %disp | if ($_v%(redir.,$1)) .timer 1 0 say $ifmatch $strip(%disp,c)
   n311=action if ($1 ischan) $iif($show,describe,.describe) $1- | else me $1-
   n312=me if ($validwin) && ($1-) describe $active $1-
   n313=
   n314=ame if ($nochans) || (!$1) return | .ame $1- | if ($show == $false) return | var %ame_x = $chan(0) | while (%ame_x) { echo $colour(own) -ti2 $chan(%ame_x) $display(me,$_ns($me,$chan(%ame_x)),$1-) | dec %ame_x }
   n315=amsg if ($nochans) || (!$1) return | .amsg $1- | if ($show == $false) return | var %amsg_x = $chan(0) | while (%amsg_x) { echo $colour(own) -ti2 $chan(%amsg_x) $iif($prefixownmsgs,$display(say,$_align($_ns($me,$chan(%amsg_x))),$1-),> $1-) | dec %amsg_x }
   n316=atopic if ($nochans) || (!$1) return | var %n = $chan(0) | while (%n) { if ($me isop $chan(%n)) || (t !isin $chan(%n).mode) .raw TOPIC $chan(%n) : $+ $1- | dec %n }
   n317=amode if ($nochans) || (!$1) return | var %n = $chan(0) | while (%n) { if ($me isop $chan(%n)) .raw MODE $chan(%n) $1- | dec %n }
   n318=
   n319=quit if (!$server) return | .quit $_quitmsg($1-)
   n320=nick _var /nick $1 | $iif($show,nick,.nick) $1-
   n321=
   n322=nw {
   n323=  if ($1) var %n = $1
   n324=  elseif ($active ischan) || ($query($active)) var %n = $active
   n325=  else return
   n326=  var %w = $+(@Notice ,%n)
   n327=  if (!$window(%w)) {
   n328=    window -ke %w $_gcoord(notice,-1 -1 350 280) @nw
   n329=    echo %w *** Whatever you type will be sent as notice to %n $iif($address(%n,2), ( $+ $address(%n,1) $+ ) )
   n330=    linesep %w
   n331=  }
   n332=}
   n333=rawnotice .raw NOTICE $1 : $+ $2-
   n334=notice {
   n335=  var %msg = $iif($2 != $null,$2-,$inp(What notice?~Notices,1))
   n336=  if ($_jchan($1) ischan) {
   n337=    var %tn = $_jchan($1), %flags = $remove($1,%tn)
   n338=    set -u10 $v%(lastnoticed.,$1) %msg
   n339=    echo $colour(own) -qa $ech(cnotices,%flags,%tn,$2-)
   n340=    rawnotice $1 %msg
   n341=  }
   n342=  else { var %tn = $nc($1) | .raw NOTICE %tn : $+ %msg | echo $colour(own) -qa $display(mynotice,%tn,%msg) }
   n343=  if ($_v%(redir.,%tn)) .timer 1 0 say $ifmatch - $+ $me $+ : $+ $1 $+ - $strip(%msg,c)
   n344=}
   n345=n if (($left($1,1) isin #&%) && (!$2)) || ((!$1) && (#)) names $iif($1,$1,#) | else notice $1-
   n346=onotice $iif($show,notice,.notice) @ $+ # $1-
   n347=on $iif($show,notice,.notice) @ $+ # $1-
   n348=ovnotice $iif($show,notice,.notice) @+ $+ # $1-
   n349=ovn $iif($show,notice,.notice) @+ $+ # $1-
   n350=_ovn.manual {
   n351=  if (!$2-) return
   n352=  var %c = $_jchan($1)
   n353=  if (@ isin $1) var %f = o
   n354=  :start
   n355=  if (%f) {
   n356=    var %i = 1
   n357=    while ($nick(%c,%i,%f)) { var %n = $ifmatch | if (%n != $me) .notice %n ( $+ $1 $+ ) $2- | inc %i }
   n358=  }
   n359=  if (+ isin $1) && (%f2 != 1) { var %f = v, %f2 = 1 | goto start }
   n360=}
   n361=vn $iif($show,notice,.notice) + $+ # $1-
   n362=cn $iif($show,notice,.notice) # $1-
   n363=cnotice {
   n364=  if (# !ischan) || (- !isin $1) return | if ($nick(#,0,a) == 1) { ale You're alone on # $+ ... | return }
   n365=  if ($2) var %msg = $2- | else var %msg = $inp(What notice?~Notices,1)
   n366=  var %1 = $remove($1,-) | :s
   n367=  if (a isin %1) var %tm = a, %1 = $remove(%1,a)
   n368=  elseif (o isin %1) && (v isin %1) var %mn = @+, %1 = $remove(%1,o,w)
   n369=  elseif (o isin %1) var %mn = @, %1 = $remove(%1,o,w)
   n370=  elseif (v isin %1) { var %tm = v, %1 = $remove(%1,v) | if (!%mn) var %mn = + }
   n371=  elseif (n isin %1) var %tm = r, %mn = nonops:, %1 = $remove(%1,n)
   n372=  :n | var %m = $+($chr(40),%mn,#,$chr(41))
   n373=  if ($show) && (!%ov) { echo $colour(own) -a $ech(cnotices,%mn,#,%msg) | var %ov = 1 }
   n374=  if (%mn == @+) { rawnotice @+ $+ # %msg | var %1 = $remove(%1,v) }
   n375=  elseif (@ isin %mn) && (($nick(#,0,o) > 1) || ($me !isop #)) rawnotice @ $+ # %m %msg
   n376=  elseif (%tm == a) rawnotice # %msg
   n377=  else {
   n378=    var %_n = 1 | while ($nick(#,%_n, [ %tm ] )) { var %ni = $ifmatch | if ($len(%l) <= 920) && (%ni != $me) var %l = $addtok(%l,%ni,44) | inc %_n }
   n379=    if (%l) mstuff %l .raw NOTICE §£ : $+ %m %msg
   n380=  }
   n381=  set -u10 $v%(lastnoticed.,$+(%mn,#)) %msg
   n382=  unset %mn | if (%1) goto s
   n383=}
   n384=mnotice mstuff $1 rawnotice §£ [snicks: $+ # $+ ] $2- | ae $ech(cnotices,snicks:,#,$2-)
   n385=xnotice var %i = 1 | while (%i <= $nick(#,0)) { var %n = $nick(#,%i) | if (%n != $me) && ($istok($1,%n,44) == $false) var %l = $addtok(%l,%n,44) | inc %i } | mstuff %l rawnotice §£ [xcept: $+ # $+ ] $2- | ae $ech(cnotices,xcept:,#,$2-)
   n386=fnotice if ($1 ischan) var %c = $1, %m = $2, %r = $3- | else var %c = $active, %m = $1, %r = $2- | var %i = $ialchan(%m,%c,0) | while (%i) { if ($ialchan(%m,%c,%i).nick != $me) var %l = $addtok(%l,$ifmatch,44) | dec %i } | mstuff %l rawnotice §£ %r
   n387=mstuff var %i = 1 | while ($gettok($1,%i,44)) { var %n = $ifmatch, %l = $addtok(%l,%n,44) | if ($numtok(%l,44) == %§.nets-max.events) { $replace($2-,§£,%l) | unset %l } | inc %i } | if (%l) $replace($2-,§£,%l)
   n388=;
   n389=_gainop if (!%§.s^n) && ($hget(x_sets,rejoingainops)) cycle $1
   n390=_comg if ($1 == $me) return | var %x = $comchan($1,0) | while (%x) { if ($comchan($1,%x).op) $replace($2-,§§,$comchan($1,%x)) | dec %x }
   n391=_comg2 if ($1 == $me) return | var %x = $chan(0) | while (%x) { if ($chan(%x).op) $replace($2-,§§,$chan(%x)) | dec %x }
   n392=gb _comg $1 .raw MODE §§ -o+b $1 $_address($1,$hget(x_prot,defban))
   n393=gk _comg $1 qkick §§ $1 $iif($2,$2-,$me)
   n394=gkb _comg $1 qkickb §§ $1 $iif($2,$2-,$me)
   n395=gfk _comg2 fk §§ $1 $iif($2,$2-,$me)
   n396=gfkb _comg2 fkb §§ $1 $iif($2,$2-,$me)
   n397=fk {
   n398=  if ($1 ischan) var %c = $1, %m = $2, %r = $iif($3,$3-,filter kick of %m) | else var %c = $active, %m = $1, %r = $iif($2,$2-,filter kick of %m)
   n399=  if ($me !isop %c) return
   n400=  var %k = $ialchan(%m,%c,0)
   n401=  while (%k) {
   n402=    var %n = $ialchan(%m,%c,%k).nick
   n403=    if (%n ison %c) && (%n != $me) && (($1 !ischan) || ($kickexempt(%n,%c))) {
   n404=      inc %§.kcount
   n405=      var %l = $addtok(%l,%n,44) | if ($numtok(%l,44) == %§.nets-max.kicks) {
   n406=        var %kc = %kc $+ KICK %c %l : $+ %r $+ $lf
   n407=        unset %l
   n408=      }
   n409=    }
   n410=    if ($count(%kc,$lf) >= 5) { .raw %kc | unset %kc } | dec %k
   n411=  }
   n412=  if (%l) var %kc = %kc $+ KICK %c %l : $+ %r | if (%kc) .raw %kc
   n413=}
   n414=fkb if ($1 ischan) var %c = $1, %m = $2, %r = $3- | else var %c = $active, %m = $1, %r = $2-, %1 = 1 | if ($me !isop %c) return | _fdeopban %c %m %1 | fk %c %m $iif(%r,%r,filter kickban of %m)
   n415=fkn if ($active ischan) fk # $address($nc($1,1).chan,$iif($2 isnum,$2,2))
   n416=multi {
   n417=  if (!$1) return | if ($1 ischan) var %c = $1, %m = $2, %n = $3, %r = $4- | else var %c = #, %m = $1, %n = $2, %r = $3- | var %i = 1, %r = : $+ $iif(%r,%r,$rkmsg)
   n418=  window -h @multi.1 | window -h @multi.2
   n419=  while (%i <= $numtok(%n,44)) {
   n420=    var %nick = $gettok(%n,%i,44)
   n421=    if (%nick == $me) goto n
   n422=    if (b isin %m) { if (%nick isop %c) var %mo = %mo $+ -o+b, %ban = %ban %nick $_address(%nick,$hget(x_prot,defban)) | else var %mo = %mo $+ b, %ban = %ban $_address(%nick,$hget(x_prot,defban)) | if ($len($remove(%mo,+,-)) >= $calc(%§.nets-max.modes -1)) { aline @multi.1 MODE %c $iif($left(%mo,1) != -,$+(+,%mo),%mo) %ban | unset %ban %mo } }
   n423=    if (%m == kill) .raw kill %nick %r
   n424=    elseif (k isin %m) {
   n425=      if (%nick ison %c) {
   n426=        var %k = $addtok(%k,%nick,44) | inc %§.kcount
   n427=        if ($numtok(%k,44) == %§.nets-max.kicks) { var %kc = %kc $+ KICK %c %k %r $+ $lf | unset %k }
   n428=      }
   n429=      if ($count(%kc,$lf) >= 5) { aline @multi.2 %kc | unset %kc }
   n430=    }
   n431=    :n | inc %i
   n432=  }
   n433=  if (%ban) aline @multi.1 MODE %c $iif($left(%mo,1) != -,$+(+,%mo),%mo) %ban
   n434=  if (%k) var %kc = %kc $+ KICK %c %k %r | if (%kc) aline @multi.2 %kc
   n435=  var %n = 1 | while (%n <= 2) {
   n436=    var %w = $+(@multi.,%n), %i = 1 | while (%i <= $line(%w,0)) { .raw $line(%w,%i) | inc %i } | inc %n
   n437=  }
   n438=  close -@ @multi.1 | close -@ @multi.2
   n439=}
   n440=;
   n441=_process.tempbans {
   n442=  if ($me !isop $1) || ($_v%(process.tempbans.,$1)) return
   n443=  if ($2) {
   n444=    var %item = $+($1,:,$2), %data = $hget(x_tempbans,%item), %time = $iif($calc(%data - $ctime) > 0,$ifmatch,0)
   n445=    if (%data) .timer $+ %item 1 %time _unb $1-
   n446=    return
   n447=  }
   n448=  var %n = 1
   n449=  while (%n <= $ibl($1,0)) {
   n450=    var %banmask = $ibl($1,%n), %item = $+($1,:,%banmask), %data = $hget(x_tempbans,%item), %time = $iif($calc(%data - $ctime) > 0,$ifmatch,0)
   n451=    if ($gettok($ibl($1,%n).by,1,33) == $me) && (%data) .timer $+ %item 1 %time _unb $1 %banmask
   n452=    inc %n
   n453=  }
   n454=  set $v%(process.tempbans.,$1) 1
   n455=}
   n456=;
   n457=enforceall var %n = 1 | while ($chan(%n)) { var %c = $ifmatch | if ($me isop %c) enforce %c | inc %n }
   n458=enforce {
   n459=  if ($1 !ischan) || ($me !isop $1) return | var %t = $nick($1,0,a), %n, %an, %l
   n460=  _chanidle $1
   n461=  while (%t) {
   n462=    %n = $nick($1,%t,a) | if (%n == $me) goto next
   n463=    if ($address(%n,5)) %an = $ifmatch | else %an = %n $+ !*@*
   n464=    %l = $remove($level($ulist(%an)),=)
   n465=    if ($istok(%l,shit,44)) qkickb $1 %n $_km(c,shitlist,$ulist(%an).info)
   n466=    elseif ($_userchlist(%an,$1)) {
   n467=      if ($istok(%l,ban,44)) { qkickb $1 %n $_km($1,banned,$ulist(%an).info) | goto next }
   n468=      elseif (($istok(%l,op,44)) || ($istok(%l,opd,44))) && (%n !isop $1) .raw mode $1 +o %n
   n469=      elseif (($istok(%l,voice,44)) || ($istok(%l,voiced,44))) && (%n !isvoice $1) && (%n !isop $1) .raw mode $1 +v %n
   n470=      elseif ($istok(%l,deop,44)) && (%n isop $1) .raw mode $1 -o %n
   n471=      elseif ($ircx) && ($istok(%l,owner,44)) && (%n !isowner $1) .raw mode $1 +q %n
   n472=    }
   n473=    elseif ($cset($1,badhostmask) != 0) {
   n474=      if ($hget(x_prot,only.badnick)) { if ($checkbw(%n)) var %do.it = 1 }
   n475=      elseif ($checkbw($replace($remove($gettok(%an,1,64),~),!,$chr(32)))) var %do.it = 1
   n476=      if (%do.it) penalty badhostmask %n $1 $_km($1,badhostmask)
   n477=    }
   n478=    elseif (guest* iswm %n) && ($cset($1,guests)) penalty guests %n $1 $_km($1,guests)
   n479=    :next
   n480=    dec %t
   n481=  }
   n482=}
   n483=;
   n484=letpass if (($_v%(noprotect.,$2)) || ($cset($2,disable)) || ($_exempt($1))) return 1
   n485=_exempt {
   n486=  var %tlev = $remove($level($ulist($1)),=) , %h = $numtok(%tlev,44)
   n487=  while (%h) {
   n488=    var %b = $gettok(%tlev,%h,44)
   n489=    if ((%b isnum 11-14) && (($istok($ulist($2).info,#,44)) || (#* !iswm $ulist($2).info))) || ($istok(10.20.bot,%b,46)) return %b
   n490=    dec %h
   n491=  }
   n492=}
   n493=kickexempt if (($hget(x_prot,kickops)) || ($1 !isop $2)) && (($hget(x_prot,kickvoices)) || ($1 !isvoice $2)) && (($hget(x_prot,kickhalfops)) || ($1 !ishop $2)) && (($hget(x_prot,kicknotify)) || ($nick !isnotify)) return 1
   n494=;
   n495=penalty {
   n496=  if ($istok($+(%§.bnicks %§.last.penalties),$2,32)) || ($_v%(lastpenalty.,$1,.,$2,.,$3)) return
   n497=  set -u1 %§.last.penalties $addtok(%§.last.penalties,$2,32)
   n498=  var %mt = %§.nets-clonetype, %a = $dll(sys\dlls\aircdll.dll,FloodDel,$+(flood.,$3,.*.,$address($2,2)))
   n499=  if ($istok(clones.ctcp.kick.ban.deop.joins,$1,46)) var %n = $address($2,2)
   n500=  else var %n = $2
   n501=  if ($_v%(pen.,$3,.,$1,.,%n)) { if (($1 == clones) && ($ialchan(%n,$3,0) > $cset($3,clones_max))) || ($1 != clones) inc $v%(pen.,$3,.,$1,.,%n) }
   n502=  else set -u $+ $calc($hget(x_prot,offense_time) *60) $v%(pen.,$3,.,$1,.,%n) 1
   n503=  :start
   n504=  if ($hget($+(x_prot-,$3),$+(pen.,$1))) var %pp = $ifmatch
   n505=  else var %pp = $hget(x_penalties,$1)
   n506=  if (!%pp) return
   n507=  var %p = $gettok(%pp,$_v%(pen.,$3,.,$1,.,%n),44)
   n508=  set -u5 %§.last-penalty $addtok(%§.last-penalty,$2,44)
   n509=  if (%p == warn_me) {
   n510=    ae $ech(localwarn,$2,$1,$3)
   n511=    if ($active != $3) echo $3 $ech(localwarn,$2,$1,$3)
   n512=    var %y = $3 $2 $4-
   n513=    set -u60 %§.warnme $addtok(%§.warnme,%y,164)
   n514=    beep 3 100
   n515=  }
   n516=  elseif (warn isin %p) {
   n517=    if (chan isin %p) .msg $3  $+ $2 $+ : $ech(customwarn,$1,$3)
   n518=    elseif ($1 == clones) fnotice $3 %n $ech(clonewarn,$3)
   n519=    elseif ($1 == guests) rawnotice $2 $ech(guestwarn,$3)
   n520=    elseif ($1 == badword) rawnotice $2 $ech(badwordwarn,$3)
   n521=    else rawnotice $2 $ech(customwarn,$1,$3)
   n522=    echo $3 $ech(localwarn3,$iif(chan isin %p,$+($2,[,$3,]),$iif($1 == clones,%n,$2)),$1)
   n523=  }
   n524=  elseif (%p == deop) .raw mode $3 -o $2
   n525=  elseif (%p == ban) .ban $3 $_address($2,$hget(x_prot,defban))
   n526=  elseif (%p == tempban) { var %time = $calc($hget(x_prot,tempban) *60), %banmask = $_address($2,$hget(x_prot,defban)) | .ban -u $+ %time $3 %banmask | hadd -m x_tempbans $+($3,:,%banmask) $calc($ctime + %time) }
   n527=  elseif (%p == kick) _ff qkick $3 $2 $4-
   n528=  elseif (%p == filterkick) _ff fk $3 $address($2,%mt) $4-
   n529=  elseif (%p == filterkickban) fkb $3 $address($2,%mt) $4-
   n530=  elseif (%p == clonekick) _ff clones $3 -k %mt
   n531=  elseif (%p == clonekickban) clones $3 -kb %mt
   n532=  elseif (%p == sitekickban) fkb $3 $address($2,2) $4-
   n533=  elseif (%p == tempkickban) qtkickb $3 $2 $4-
   n534=  elseif (%p == kickban) qkickb $3 $2 $4-
   n535=  elseif (%p == guestkickban) guestkickban $3
   n536=  elseif (custom:* iswm %p) { ae $ech(localwarn4,$2,$1) | _perfd $replace($gettok(%p,2-,58),&nick,$2,&#,$3,&chan,$3,·,$chr(44),&me,$me,&pen,$1) }
   n537=  else {
   n538=    set -u $+ $calc($hget(x_prot,offense_time) *60) $v%(pen.,$3,.,$1,.,%n) $iif($1 == clones,1,$calc($_v%(pen.,$3,.,$1,.,%n) -1))
   n539=    goto start
   n540=  }
   n541=  if ($hget(x_prot,warnme)) && (%p != warn_me) && ($active != $3) && ($validwin) ae $ech(localwarn2,$2,%p,$1,$3)
   n542=}
   n543=text.protect {
   n544=  if ($me !isop $2) || ($letpass($address($1,5),$2)) || ($kickexempt($1,$2) != 1) return
   n545=  var %len = $len($4-), %lag = $remove(%§.lag,sec,s,+)
   n546=  if ($3 == lines) && (*slaps*around* iswm $4-) && ($hget(x_prot,slapkick) != 0) && (+flood* iswm $dll(sys\dlls\aircdll.dll,Flood,slapkick 1 3 10)) qkick $2 $1 $_km($2,slaps)
   n547=  elseif (($cset($2,codes)) && ($calc($count($4-,) + $count($4-,) + $count($4-,) + $count($4-,)) >= $cset($2,codes_max))) penalty codes $1-2 $_km($2,codes)
   n548=  elseif (($cset($2,colours)) && ($count($4-,) >= $cset($2,colours_max))) penalty colours $1-2 $_km($2,colours)
   n549=  elseif (($cset($2,caps)) && ($calc(%len - $len($removecs($4-,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z))) >= $cset($2,caps_max))) penalty caps $1-2 $_km($2,caps)
   n550=  elseif ($_filter($2,advertise,$4-)) penalty advertise $1-2 $_km($2,advertise)
   n551=  elseif ($cset($2,badword)) && ($checkbw($4-)) penalty badword $1-2 $_km($2,badword,$replace($ifmatch,a,*,e,*,i,*,o,*,u,*))
   n552=  elseif (%lag !isnum) || (%lag < 10) {
   n553=    if ($mflood($site)) {
   n554=      echo $2 $ech(mass,$3,$2)
   n555=      ignore -ncu3 *!*@*
   n556=      if ($hget(x_prot,mflood_action)) _perfd $ifmatch
   n557=      else mode $2 +iml $nick($2,0)
   n558=    }
   n559=    elseif ($sflood(bytes,$address($1,2),$2,1,%len)) penalty bytes $1-2 $_km($2,bytes)
   n560=    elseif ($cset($2,chars)) && (%len > $cset($2,chars_max)) penalty chars $1-2 $_km($2,chars)
   n561=    elseif ($sflood(repeat,$1,$2,$4-)) penalty repeat $1-2 $_km($2,repeat)
   n562=    elseif ($sflood($3,$address($1,2),$2,1)) {
   n563=      if ($3 == lines) penalty lines $1-2 $_km($2,lines)
   n564=      elseif ($3 == notice) penalty notice $1-2 $_km($2,notice)
   n565=    }
   n566=  }
   n567=}
   n568=_filter {
   n569=  if ($istok(spam.advertise,$2,46)) && ($cset($1,$2) != 1) return
   n570=  var %line = $_rpont2($3-), %s = x_ $+ $2
   n571=  if ($2 == advertise) && ($istok(%line,$1,32)) return
   n572=  if ($hget(%s,$hmatch(%s,%line,1).data)) return 1
   n573=}
   n574=_rpont2 return $remove($replace($_rpont($replace($1-,:,¨)),¨,:),*)
   n575=checkbw {
   n576=  var %wl = $hget(x_prot,badword.list), %line = $_rpont($1-), %i = 1, %w
   n577=  while ($gettok(%wl,%i,44)) {
   n578=    %w = $ifmatch
   n579=    if ($istok(%line,%w,32)) || ($wildtok(%line,%w,1,32)) return %w
   n580=    inc %i
   n581=  }
   n582=}
   n583=checkbc {
   n584=  var %cl = $hget(x_prot,badchannel.list), %n = 1, %line = $remtok($1-,$2,32)
   n585=  while ($gettok(%cl,%n,44)) {
   n586=    var %l = $ifmatch, %w = $gettok(%l,1,59)
   n587=    if ((%w iswm %line) || (%w isin %line)) && (%w !isin $2) return $gettok(%l,-1,59)
   n588=    inc %n
   n589=  }
   n590=}
   n591=bcscan {
   n592=  if ($1 == stop) { _--unset vars bcscan | return }
   n593=  var %n = $nick(#,0), %l
   n594=  if (%n >= 50) { sr -t Error Too many users to scan | return }
   n595=  while (%n) { %l = $addtok(%l,$nick(#,%n),44) | dec %n }
   n596=  ale Scanning users on # for badchannels
   n597=  _bchanwi # $gettok(%l,1,44)
   n598=  _var bcscan $deltok(%l,1,44)
   n599=}
   n600=_bchanwi if ($2 ison $1) { set $v%(bchan.,$2) $1 | .raw WHOIS $2 }
   n601=
   n602=idlekick {
   n603=  var %c = $iif($1 ischan,$1,#), %p = $iif($1 ischan,$2,$1), %t = $iif($3,$3,$iif($2,$2,$1))
   n604=  if ($me !isop %c) return
   n605=  if (%t !isnum) var %t = $inp(Kick users with idle above ... ? (in mins)~Idle Kick,1)
   n606=  if ($window($+(@Whois ,%c))) _idlekick $+(@Whois ,%c) %c %t %p
   n607=  else { set $v%(idlekick.,%c) %t %p | whoislist 0 }
   n608=}
   n609=_idlekick {
   n610=  var %n = 5, %k = 0 | while ($line($1,%n)) {
   n611=    var %l = $ifmatch, %u = $gettok(%l,1,9)
   n612=    if (%u == $me) goto n
   n613=    var %i = $int($calc($_aduration($gettok(%l,3,9)) /60))
   n614=    if (%i >= $3) && ($kickexempt(%u,$2)) && (%u ison $2) {
   n615=      $iif($4 == b,qkickb,qkick) $2 %u $_km($2,idle,$duration($calc(%i *60)))
   n616=      inc %k
   n617=    }
   n618=    :n
   n619=    inc %n
   n620=  }
   n621=  if ($_v%(idlekick.,$2)) && ($window($1)) { unset $v%(idlekick.,$2) | window -c $1 }
   n622=  echo $2 $logo Idle scan completed ( $+ %k $iif(%k > 1,users,user) idleing > $3 $+ mins)
   n623=}
   n624=
   n625=p.ignore $iif($hget(x_prot,silence),silence,ignore) $1-
   n626=
   n627=;flood routines (using tabo's aircdll.dll)
   n628=
   n629=;personal flood
   n630=pflood {
   n631=  if ($hget(x_sets,ultraflood) != 0) {
   n632=    var %m = $iif($hget(x_prot,ultra_max),$ifmatch,25), %t = $iif($hget(x_prot,ultra_time),$ifmatch,5)
   n633=    if (+flood* iswm $dll(sys\dlls\aircdll.dll,Flood,ultraflood 1 %m %t)) {
   n634=      ae $ech(ultraflood,$2)
   n635=      if ($hget(x_prot,ultra_action)) _perfd $ifmatch
   n636=      else p.ignore -ntiu15 *!*@*
   n637=      halt
   n638=    }
   n639=  }
   n640=  if ($hget(x_sets,floodprot)) && ($sflood($1,$2,$3)) return 1
   n641=}
   n642=
   n643=;mass flood
   n644=mflood {
   n645=  if ($hget(x_prot,nomassflood)) return
   n646=  if ($hget(x_tmp-cache,mfloodsets)) var %m = $gettok($ifmatch,1,32), %t = $gettok($ifmatch,2,32), %ms = $gettok($ifmatch,3,32),
   n647=  else {
   n648=    var %m = $iif($hget(x_prot,mflood_clmax) isnum,$ifmatch,20), %t = $iif($hget(x_prot,mflood_cltime) isnum,$ifmatch,4), %ms = $int($calc(($iif($hget(x_prot,mflood_clmusites),$ifmatch,50) /100) * %m))
   n649=    hadd -mu $+ %t x_tmp-cache mfloodsets %m %t %ms
   n650=  }
   n651=  var %fs = $hget(x_vars,mfloodsites), %fl = $gettok($dll(sys\dlls\aircdll.dll,Flood,mflood 1 %m %t),2,32)
   n652=  if (%fl == 1) hadd -mu $+ %t x_vars mfloodsites $1
   n653=  elseif ($len(%fs) <= 800) hadd -mu10 x_vars mfloodsites $qtok(%fs,$1,20,44)
   n654=  if (%fl >= %m) && ($numtok(%fs,44) < %ms) {
   n655=    _--unset vars mfloodsites
   n656=    return 1
   n657=  }
   n658=}
   n659=
   n660=;allround flood routine
   n661=sflood {
   n662=  if ($prop == mass) && (($_v%(noprotect.,$3)) || ($cset($3,disable)) || ($kickexempt($4,$3) != 1)) return
   n663=  if ($hget(x_tmp-cache,$+($3,.,$1,.,$2))) var %s = $gettok($ifmatch,1,32), %t = $gettok($ifmatch,2,32), %m = $gettok($ifmatch,3,32)
   n664=  else {
   n665=    var %s = $cset($3,$1), %t = $cset($3,$1_time), %m = $cset($3,$1_max)
   n666=    hadd -mu $+ %t x_tmp-cache $+($3,.,$1,.,$2) %s %t %m
   n667=  }
   n668=  if (%s) && (%t) && (%m) {
   n669=    if ($prop == mctcp) %m = $calc(%m *3)
   n670=    if ($1 == repeat) && ($_v%(repeatline.,$3,.,$2) != . $4-) {
   n671=      set -u $+ %t $v%(repeatline.,$3,.,$2) . $4-
   n672=      var %a = $dll(sys\dlls\aircdll.dll,FloodDel,$+(flood.,$3,.repeat.,$2))
   n673=    }
   n674=    if (+flood* iswm $dll(sys\dlls\aircdll.dll,Flood,$+(flood.,$3,.,$1,.,$2) 1 %m %t)) return 1
   n675=  }
   n676=}
   n677=;
   n678=;EOF
